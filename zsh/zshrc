# Main zsh options
HISTFILE=~/.local/state/zsh_history
HISTSIZE=5000
SAVEHIST=5000

bindkey -e #emacs mode
setopt autocd #cd by typing dir name
unsetopt beep #no beeping!
autoload -Uz compinit
compinit

# Conditional ubuntu config
if [[ "$($HOME/dev/dotfiles/sys id)" == "ubuntu" ]]; then
    alias fd='fdfind'
fi

# Brew
if [[ -x "/home/linuxbrew/.linuxbrew/bin/brew" ]]; then
    eval "$(/home/linuxbrew/.linuxbrew/bin/brew shellenv)"
fi


# FZF configuration
source <(fzf --zsh)
# Unbind the default Alt-C keybinding for fzf-cd-widget
bindkey -rM emacs '^[^C'
bindkey -rM vicmd '^[^C'
bindkey -rM viins '^[^C'
# Bind fzf-cd-widget to Ctrl-G
bindkey -M emacs '^H' fzf-cd-widget
bindkey -M vicmd '^H' fzf-cd-widget
bindkey -M viins '^H' fzf-cd-widget
export FZF_DEFAULT_OPTS="\
--color=bg+:#313244,spinner:#F9E2AF,hl:#F38BA8 \
--color=fg:#CDD6F4,header:#A6E3A1,info:#89B4FA,pointer:#F9E2AF \
--color=marker:#94E2D5,fg+:#CDD6F4,prompt:#FAB387,hl+:#F38BA8 \
--color=selected-bg:#45475A \
--color=border:#6C7086,label:#89B4FA \
--height 20% \
--info=inline \
--layout=reverse"

# Fallback prompt
PS1="%{%F{green}%}%n@%m %{%F{yellow}%}%2~ %{%F{reset}%}%% "

# Aliases
alias ts='tailscale'
alias calc='qalc'
alias bc='bc -l'
alias srcz='source ~/.config/zsh/.zshrc'
alias gs='git status'
alias gd='git diff'
alias ls='eza'
alias la='eza -la'
alias lst='eza --tree'
alias grep='grep --color=auto'
alias vi='nvim'
alias iv='nvim' #common typo
alias vim='nvim'
alias fs='fastfetch'
alias fsa='fastfetch -c all.jsonc'
alias c='clear'
alias ai='opencode'
alias ia='opencode'
alias wx='curl wttr.in'
alias rm='trash-put'
alias cat='bat'
alias mv='mv -i'
alias cp='cp -i'
alias showpath='echo $PATH | tr ":" "\n"'
alias df="df -h"
alias du="du -h -d 1"
alias asciiquarium="asciiquarium -t"
alias venv="source .venv/bin/activate"
#alias -g C='| wl-copy' #somehwat risky

# Tmux-sessionizer
bindkey -s '^t' "tmux-sessionizer\n"
bindkey -s '\eh' "tmux-sessionizer -s 0\n"
bindkey -s '\ej' "tmux-sessionizer -s 1\n"
bindkey -s '\ek' "tmux-sessionizer -s 2\n"
bindkey -s '\el' "tmux-sessionizer -s 3\n"

# Because useful C-f bind conflicts with my tmux prefix, rebind forward-char to C-g
bindkey '^G' forward-char

# Hitting space will expand stuff like !!
bindkey ' ' magic-space

# keybind for typing boilerplate
bindkey -s '^wgc' 'git commit -m ""\C-b'
bindkey -s '^wi' "opencode run ''\C-b"

# Copy buffer contents to clipboard
copy-buffer-to-clipboard() {
   echo -n "$BUFFER" | wl-copy
   zle -M "Copied to clipboard"
}
zle -N copy-buffer-to-clipboard
bindkey '^wc' copy-buffer-to-clipboard

# Open buffer line in editor
autoload -Uz edit-command-line
zle -N edit-command-line
bindkey '^w^e' edit-command-line

# Zoxide
eval "$(zoxide init --cmd cd zsh)"

# Run yazi with 'y', change cwd when exiting yazi
y() {
	local tmp="$(mktemp -t "yazi-cwd.XXXXXX")" cwd
	yazi "$@" --cwd-file="$tmp"
	IFS= read -r -d '' cwd < "$tmp"
	[ -n "$cwd" ] && [ "$cwd" != "$PWD" ] && builtin cd -- "$cwd"
	rm -f -- "$tmp"
}

# Automtically activate/deactivate python .venv
auto_venv() {
  # 1. Find the nearest venv by climbing up
  local dir="$PWD"
  local found_venv=""
  while [[ "$dir" != "/" ]]; do
    if [[ -f "$dir/.venv/bin/activate" ]]; then
      found_venv="$dir/.venv"
      break
    fi
    dir="${dir:h}"
  done

  # 2. If we are in a venv that DOESN'T match what we found (or we found nothing)
  if [[ -n "$VIRTUAL_ENV" && "$VIRTUAL_ENV" != "$found_venv" ]]; then
    if typeset -f deactivate > /dev/null; then
      deactivate
    else
      unset VIRTUAL_ENV
    fi
  fi

  # 3. If we found a venv and it's not already the active one, source it
  if [[ -n "$found_venv" && "$VIRTUAL_ENV" != "$found_venv" ]]; then
    source "$found_venv/bin/activate"
  fi
}
autoload -U add-zsh-hook
add-zsh-hook chpwd auto_venv
auto_venv # Run once on startup


# --- Conditional macOS config ---
if [[ "$(uname)" == "Darwin" ]]; then
    alias rm='trash'
fi

# Finally, the eyecandy
eval "$(starship init zsh)"
precmd() { printf "\e[5 q" } # use bar-shaped cursor
fastfetch
